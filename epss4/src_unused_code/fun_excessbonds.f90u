module fun_excessbonds
    use kinds           ,only: dp
    use aggregate_grids_class ,only: tAggGrids
    implicit none

    private
    type(tAggGrids)                  :: agg_grid
    real(dp)                         :: nwaget, penst, rt, rft
    real(dp) ,dimension(:,:,:,:), allocatable :: apgrid_zk, stocks_zk, xgrid_zk  ! policies for given z and K
    real(dp) ,dimension(:,:,:), allocatable   :: apgridm, stocksm  ! policies for given z, K, and mu last period (minus)
    real(dp) ,dimension(:,:,:), allocatable   :: Phim             ! distribution
    real(dp) ,dimension(:), allocatable       :: etagridt

    public f_excessbonds, set_excessbondsvars

    interface set_excessbondsvars
        module procedure set_excessbondsvars_end_xgrid, set_excessbondsvars_ex_xgrid
    end interface

contains
!-------------------------------------------------------------------------------
! Module procedures in order:
! - pure function f_excessbonds(mut)
! - subroutine set_excessbondsvars(wage,pens,r,rf,apgrid_minus, stocks_minus, apgrid, stocks ,xgrid, aggr ,Ph, etagri)
! - pure subroutine get_excessbondsvars(apgrid, stocks, aggr ,Ph)
!-------------------------------------------------------------------------------

	pure real(dp) function f_excessbonds(mut)
	! calculates excess bond demand
	    use params_mod    ,only: L_N_ratio, exogenous_xgrid, de_ratio
	    use fun_locate
	    use distribution ,only: TransitionPhi
    !    use bs3vl_int   ! IMSL Math.pdf, p. 754ff: evaluate a 3d tensor-product spline given B-spline-coeffs

	    real(dp) ,intent(in) :: mut       ! expected equity premium
        real(dp) ,dimension(:,:,:) ,allocatable :: Phi, apgridt, stockst, xgridt    ! distribution, policies for given z, K, AND mu
	    real(dp)                         :: w, bond_supply
	    integer                          :: i

        if (size(agg_grid%mu) >1) then
		    i        = f_locate(agg_grid%mu, mut)
		    w        = (mut - agg_grid%mu(i)) / (agg_grid%mu(i+1) - agg_grid%mu(i))

	        if (.not. exogenous_xgrid) then
		        xgridt   = (1-w)* xgrid_zk(:,:,:,i) + w* xgrid_zk(:,:,:,i+1)
		        Phi      = TransitionPhi(rft,rt,nwaget,penst,xgridt,apgridm,stocksm,etagridt, Phim)
	        else
	            Phi = Phim
	        endif

		    apgridt  = (1-w)*apgrid_zk(:,:,:,i) + w*apgrid_zk(:,:,:,i+1)
		    stockst  = (1-w)*stocks_zk(:,:,:,i) + w*stocks_zk(:,:,:,i+1)

        else
            Phi = Phim
            apgridt  = apgrid_zk(:,:,:,1)
            stockst  = stocks_zk(:,:,:,1)
        endif

        bond_supply   = de_ratio * sum(stockst*Phi)/L_N_ratio
	    f_excessbonds = bond_supply - sum((apgridt - stockst)*Phi)/L_N_ratio    ! analytically, L_N_ratio drops out

	end function f_excessbonds
!-------------------------------------------------------------------------------

    subroutine set_excessbondsvars_end_xgrid(wage,pens,r,rf,apgrid_minus, stocks_minus, apgrid, stocks ,xgrid, aggr ,Ph, etagri)
    ! this one is called if (.not. exogenous_xgrid)
        real(dp)                     ,intent(in) :: wage, pens, r, rf
        real(dp), dimension(:,:,:)   ,intent(in) :: apgrid_minus, stocks_minus ! policies for given z, K, and mu last period
        real(dp), dimension(:,:,:,:) ,intent(in) :: apgrid, stocks, xgrid      ! policies for given z and K
        type(tAggGrids)              ,intent(in) :: aggr
        real(dp), dimension(:,:,:)   ,intent(in) :: Ph
        real(dp), dimension(:)       ,intent(in) :: etagri

        nwaget    = wage
        penst     = pens
        rt        = r
        rft       = rf
        apgridm   = apgrid_minus
        stocksm    = stocks_minus
        apgrid_zk = apgrid
        stocks_zk  = stocks
        xgrid_zk  = xgrid
        agg_grid  = aggr
        Phim      = Ph
        etagridt  = etagri

    end subroutine set_excessbondsvars_end_xgrid
!-------------------------------------------------------------------------------

    subroutine set_excessbondsvars_ex_xgrid(apgrid, stocks, aggr ,Ph)
    ! this one is called if (exogenous_xgrid)
        real(dp), dimension(:,:,:,:) ,intent(in) :: apgrid, stocks   ! policies for given z and K
        type(tAggGrids)              ,intent(in) :: aggr
        real(dp), dimension(:,:,:)   ,intent(in) :: Ph
        apgrid_zk = apgrid
        stocks_zk  = stocks
        agg_grid  = aggr
        Phim      = Ph
    end subroutine set_excessbondsvars_ex_xgrid
!-------------------------------------------------------------------------------

    pure subroutine get_excessbondsvars(apgrid, stocks, aggr ,Ph)
        ! This getter method might be superfluous.
        real(dp) ,dimension(:,:,:,:) ,intent(out) :: apgrid, stocks    ! policies for given z and K
        type(tAggGrids)              ,intent(out) :: aggr
        real(dp) ,dimension(:,:,:)   ,intent(out) :: Ph

        apgrid = apgrid_zk
        stocks  = stocks_zk
        aggr   = agg_grid
        Ph     = Phim

    end subroutine get_excessbondsvars

end module fun_excessbonds
